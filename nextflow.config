manifest {
    name = 'crukci-bioinformatics/nf-referencebuilder'
    author = 'Richard Bowers'
    description = 'CRUK-CI reference building pipeline. For internal use only.'
    mainScript = 'builder.nf'
    nextflowVersion = '>=23.9.0'
    recurseSubmodules = true
    version = '2.1.3'
}

executor {
    $slurm {
        queueSize = 200
        pollInterval = '30sec'
        queue = 'general'
        clusterOptions = "--nodes=1 --open-mode=truncate"
        jobName = { "'${task.process.split(/:/).last()} ${task.tag}'" }
    }
}

singularity.enabled = true
singularity.autoMounts = true
singularity.runOptions = "--bind '${projectDir}'"

process {
    cpus = 1
    memory = '1GB'
    time = '1h'

    tag = { "${genomeInfo.base}" }

    container = "crukcibioinformatics/referencebuilder:${manifest.version}"

    errorStrategy = {
        task.exitStatus in [ 104, 134, 137..140, 255 ].flatten() ? 'retry' : 'ignore'
    }

    withLabel:picard {
       memory = { 2.GB * 2 ** (task.attempt - 1) } // So 2, 4, 8, 16 etc
       time = { 8.h * task.attempt }
       maxRetries = 3
    }

    withLabel:builder {
       memory = { 8.GB * 2 ** (task.attempt - 1) } // So 8, 16, 32
       time = { 12.h * task.attempt }
       maxRetries = 2
    }

    withLabel:assembler {
       memory = { 2.GB * 2 ** (task.attempt - 1) } // So 2, 4, 8, 16
       time = { 8.h * task.attempt }
       maxRetries = 3
    }

    withLabel:fetcher {
       memory = 4.MB
       time = '1h'
       executor = 'local'
    }

    withLabel:tiny {
       memory = 4.MB
       time = '5m'
       executor = 'local'
    }
}

profiles {
    standard {
        params.referenceTop = "/data/personal/bowers01/nf_reference_data"

        process {
            executor = 'local'
        }

        executor.$local.cpus = 4
        executor.$local.memory = '32GB'
    }

    epyc {
        params.referenceTop = "/mnt/scratchc/bioinformatics/reference_data/reference_genomes_2025"

        process {
            executor = 'slurm'
            jobName = { "'ReferenceBuilder ${task.process.split(/:/).last()} ${task.tag}'" }
        }
    }

    bioinf {
        params.referenceTop = "/data/reference_data/reference_genomes_2025"

        process {
            executor = 'local'
        }

        executor.$local.cpus = 40
        executor.$local.memory = '200GB'
    }
}

params {

    // Location of properties files.

    genomeInfoDirectory = "${projectDir}/genomeinfo"

    // Software versions.

    BOWTIE2_VERSION = '2.5.4'
    BWA_VERSION = '0.7.19'
    BWAMEM2_VERSION = '2.3'
    SALMON_VERSION = '1.10.3'
    STAR_VERSION = '2.7.11b'

    // Application paths in the container.

    PICARD = "/usr/local/lib/picard.jar"
    REFBUILDER = "/usr/local/lib/nf-referencebuilder.jar"
}

timeline {
    enabled = true
    file = "${launchDir}/work/execution_timeline.html"
    overwrite = true
}
report {
    enabled = true
    file = "${launchDir}/work/execution_report.html"
    overwrite = true
}
